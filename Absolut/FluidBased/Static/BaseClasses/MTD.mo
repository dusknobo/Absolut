within Absolut.FluidBased.Static.BaseClasses;
partial model MTD "Mean temperature difference"

  Modelica.Units.SI.TemperatureDifference dT1(start=2, nominal=5) "Temperature differences 1 for HX";
  Modelica.Units.SI.TemperatureDifference dT2(start=1, nominal=5) "Temperature differences 2 for HX";
  Modelica.Units.SI.TemperatureDifference dT_Chen(nominal=5) "Chen approximation of LMTD";
  Modelica.Units.SI.TemperatureDifference dT_used(nominal=5) "Used mean temperature difference";

  Modelica.Units.SI.TemperatureDifference dT_log(nominal=5) "Logarithmic mean temperature difference (LMTD)";
  Boolean dTzero;

  parameter Boolean usedT_log = true "true to primarly use LMTD instead of Chen approximation";
  parameter Real tol = 0.1 "Tolerance for the near-equality diagnostic dTzero";
  parameter Modelica.Units.SI.TemperatureDifference dT_min=1e-6
    "Fallback for crossed temperature differences; zero/equal positive limits remain analytic";
  parameter Boolean useNoEvent = true "true to use NoEvent operator";
  parameter Boolean useHomotopy = false
    "Use arithmetic mean during homotopy initialization"
    annotation(Evaluate=true);

equation

  if useNoEvent then
  dTzero = noEvent(if not usedT_log then true elseif dT1*dT1 < dT2*dT2 - tol or dT1*dT1 - tol > dT2*dT2 then false else true);
  dT_log = if not usedT_log then 0 elseif noEvent(dT1 > 0 and dT2 > 0) then logMeanPositive(dT1,dT2) else dT_Chen;
  dT_Chen = noEvent(if dT1 >= 0 and dT2 >= 0 then ((1/2)*max(0,dT1)^0.3275 + (1/2)*max(0,dT2)^0.3275)^(1/0.3275) else dT_min);
  // The arithmetic mean helps initialization; the actual heat-transfer law is
  // restored when the homotopy parameter reaches one.
  dT_used = if useHomotopy then
    homotopy(actual=if usedT_log then dT_log else dT_Chen, simplified=(dT1+dT2)/2)
    else if usedT_log then dT_log else dT_Chen;
  else
  dTzero = false;
  dT_log = if not usedT_log then 0 elseif noEvent(dT1 > 0 and dT2 > 0) then logMeanPositive(dT1,dT2) else dT_Chen;
  dT_Chen = if noEvent(dT1 >= 0) and noEvent(dT2 >= 0) then ((1/2)*max(0,dT1)^0.3275 + (1/2)*max(0,dT2)^0.3275)^(1/0.3275) else dT_min;
  dT_used = if useHomotopy then
    homotopy(actual=if usedT_log then dT_log else dT_Chen, simplified=(dT1+dT2)/2)
    else if usedT_log then dT_log else dT_Chen;
  end if;


  annotation (Icon(coordinateSystem(preserveAspectRatio=false)), Diagram(
        coordinateSystem(preserveAspectRatio=false)));
end MTD;
